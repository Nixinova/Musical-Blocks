#---------------------------------#
#                                 #
#        MUSICAL BLOCKS v2        #
#          by Nixinova            #
#                                 #
#       World setup commands      #
#                                 #
#---------------------------------#

tellraw @a "If the map has not been set up properly, please run the command again."

# Teleport players
setworldspawn 968 5 1018
spawnpoint @a 968 5 1018
tp @a 968 5 1018

# Teams
team add lobby
team modify lobby color yellow
team modify lobby friendlyFire false
team add playing
team modify playing color green
team add dead
team modify dead color red
team modify playing friendlyFire false

# Scoreboards
scoreboard objectives add Timer dummy "Timer"
scoreboard objectives add Dead deathCount "Dead"
scoreboard objectives add Health health "Health"
scoreboard objectives add MB dummy {"text":"§1M§3U§5S§7I§9C§bA§dL §2B§4L§6O§8C§aK§cS§r"}
scoreboard objectives add Map dummy "Map"
scoreboard objectives add Wins dummy "§aWins"
scoreboard objectives add Players dummy "Players"
scoreboard objectives add Colour dummy "Colour"

# Command blocks
setblock 965 4 1041 repeating_command_block{Command:"function mb:map/running",auto:true}

# World settings
gamerule spawnRadius 2
gamerule commandBlockOutput false
gamerule doImmediateRespawn true
gamerule showDeathMessages false
gamerule doDaylightCycle false
time set day
difficulty peaceful

# Spawn
fill 975 3 1010 960 8 1026 iron_block hollow
fill 962 3 1024 967 3 1019 redstone_lamp
fill 962 3 1012 967 3 1017 redstone_lamp
fill 973 3 1024 969 3 1019 redstone_lamp
fill 969 3 1017 973 3 1012 redstone_lamp
fill 973 2 1012 962 2 1024 redstone_block
setblock 961 5 1018 player_wall_head[facing=east]{SkullOwner:{Name:"Nixinova"}}
setblock 961 5 1019 birch_wall_sign[facing=east]{Text2:'"§1M§3U§5S§7I§9C§bA§dL"',Text3:'"§2B§4L§6O§8C§aK§cS"'}
setblock 961 5 1017 birch_wall_sign[facing=east]{Text2:'{"text":"v3.0","color":"red"}'}
setblock 974 5 1018 birch_wall_sign[facing=west]{Text1:'{"color":"green","clickEvent":{"action":"run_command","value":"function mb:map/play"},"text":"=========================="}',Text2:'{"bold":true,"color":"green","text":"Start"}',Text3:'{"bold":true,"color":"green","text":"Game"}',Text4:'{"color":"green","text":"=========================="}'}
setblock 976 4 1018 command_block{Command:"function mb:map/play"}
setblock 974 4 1018 stone_button[facing=west]

# Map
fill 1027 17 947 1060 8 980 barrier hollow
fill 1027 8 947 1060 0 980 iron_block hollow
fill 1059 8 948 1028 8 979 air
fill 1059 16 948 1028 17 979 air
fill 1028 1 979 1059 1 948 lava

# Banners
_const banner_east = 'black_wall_banner[facing=east]'
_const banner_west = 'black_wall_banner[facing=west]'
_const pattern_N = '{Patterns:[{Pattern:"drs",Color:0},{Pattern:"ls",Color:0},{Pattern:"rs",Color:0},{Pattern:"ss",Color:0},{Pattern:"bo",Color:15}]}'
_const pattern_I = '{Patterns:[{Pattern:"ss",Color:0},{Pattern:"ls",Color:15},{Pattern:"rs",Color:15},{Pattern:"ts",Color:0},{Pattern:"cs",Color:0},{Pattern:"bs",Color:0},{Pattern:"bo",Color:15}]}'
_const pattern_X = '{Patterns:[{Pattern:"bs",Color:0},{Pattern:"ts",Color:0},{Pattern:"cs",Color:15},{Pattern:"dls",Color:0},{Pattern:"drs",Color:0},{Pattern:"bo",Color:15}]}'
_const pattern_O = '{Patterns:[{Pattern:"ts",Color:0},{Pattern:"bs",Color:0},{Pattern:"rs",Color:0},{Pattern:"ls",Color:0},{Pattern:"bo",Color:15}]}'
_const pattern_V = '{Patterns:[{Pattern:"ls",Color:0},{Pattern:"rd",Color:15},{Pattern:"dls",Color:0},{Pattern:"bo",Color:15}]}'
_const pattern_A = '{Patterns:[{Pattern:"ts",Color:0},{Pattern:"ms",Color:0},{Pattern:"ls",Color:0},{Pattern:"rs",Color:0},{Pattern:"bo",Color:15}]}'
setblock 1028 7 967 $(banner_east)$(pattern_N)
setblock 1028 7 966 $(banner_east)$(pattern_I)
setblock 1028 7 965 $(banner_east)$(pattern_X)
setblock 1028 7 964 $(banner_east)$(pattern_I)
setblock 1028 7 963 $(banner_east)$(pattern_N)
setblock 1028 7 962 $(banner_east)$(pattern_O)
setblock 1028 7 961 $(banner_east)$(pattern_V)
setblock 1028 7 960 $(banner_east)$(pattern_A)

setblock 1059 7 960 $(banner_west)$(pattern_N)
setblock 1059 7 961 $(banner_west)$(pattern_I)
setblock 1059 7 962 $(banner_west)$(pattern_X)
setblock 1059 7 963 $(banner_west)$(pattern_I)
setblock 1059 7 964 $(banner_west)$(pattern_N)
setblock 1059 7 965 $(banner_west)$(pattern_O)
setblock 1059 7 966 $(banner_west)$(pattern_V)
setblock 1059 7 967 $(banner_west)$(pattern_A)

# RNG by fafik77
scoreboard objectives add RNG_77 dummy "RNG"
scoreboard objectives add RNG_77_rp minecraft.custom:minecraft.walk_one_cm "RNG-data"
execute unless score #const2 RNG_77 matches 1.. run scoreboard players set #const2 RNG_77 3
scoreboard players add #prev_number RNG_77 0
scoreboard players set #m1 RNG_77 -1
scoreboard players add #clock_time RNG_77 1

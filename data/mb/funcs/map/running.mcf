#---------------------------------#
#                                 #
#       MUSICAL BLOCKS v3.2       #
#          by Nixinova            #
#                                 #
#     Always running commands     #
#                                 #
#---------------------------------#

# General
setworldspawn 967 4 1018
spawnpoint @a 967 4 1018
team join lobby @a[x=967,y=4,z=1018,distance=..10]
gamemode adventure @a[team=lobby]
effect give @a[team=lobby] instant_health 5 0 true
effect give @a[team=lobby] saturation 5 0 true
effect give @a[team=lobby] fire_resistance 1 0 true
scoreboard players add running Timer 1
execute if score running Timer matches 1000.. run scoreboard players set running Timer 0
tp @a[x=968,y=9,z=1018,distance=..2] 968 5 1018

# Death
_const deadPlayers = '@a[scores={Health=..17}]'
team join dead $(deadPlayers)
execute if entity $(deadPlayers) run tellraw @a ["",{"selector":"$(deadPlayers)"},{"text":" has been eliminated","color":"gold"}]
gamemode spectator @a[team=dead]
effect give @a[team=dead] instant_health
execute store result score Alive Players if entity @e[team=playing]
execute if score Online Players matches 2.. if score Alive Players matches ..1 if score Round MB matches 1.. run function mb:map/game_end

# Game start
execute if score starting Map matches 1.. run function mb:map/countdown
execute unless score starting Timer matches 120.. run scoreboard players add starting Timer 1

# Round countdown
_const seconds = 'score playing Map matches 1.. if score seconds Timer matches'
execute if $(seconds) 0 run title @a[team=playing] title ""
execute if $(seconds) 2 run title @a[team=playing] subtitle {"text":"3..","color":"red"}
execute if $(seconds) 3 run title @a[team=playing] subtitle {"text":"2..","color":"red"}
execute if $(seconds) 4 run title @a[team=playing] subtitle {"text":"1..","color":"red"}
execute if $(seconds) 5 run title @a[team=playing] subtitle ""
execute if $(seconds) 5 run function mb:map/round_mid
execute if $(seconds) 9 run scoreboard players set playing Map 2
execute if $(seconds) 10 run function mb:map/round_setup
execute if $(seconds) 10 run scoreboard players reset * Timer

# Clear map fragments
fill 1029 6 949 1058 16 978 air replace #wool

# Tick
_if (!'score playing Map matches 1') {
  // Randomise map
  scoreboard players set min RNG_77 1
  scoreboard players set max RNG_77 13
  function mb:rng/rand
  scoreboard players operation randomiser Map = number RNG_77
  scoreboard players operation MapID MB = randomiser Map
  // Randomise colour
  scoreboard players set min RNG_77 1
  scoreboard players set max RNG_77 5
  function mb:rng/rand
  scoreboard players operation colour Map = number RNG_77
}
_if ('score seconds Timer matches 60..') {
  scoreboard players add minutes Timer 1
  scoreboard players remove seconds Timer 60
}
_if ('score ticks Timer matches 20..') {
  scoreboard players add seconds Timer 1
  scoreboard players remove ticks Timer 20
}
scoreboard players add ticks Timer 1

# RNG by fafik77
scoreboard players add #clock_time RNG_77 1
execute if score #clock_time RNG_77 matches 992.. run scoreboard players set #clock_time RNG_77 1
